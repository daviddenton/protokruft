buildscript {
    ext.kotlin_version = '1.2.40'

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.3'
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.6"
        classpath 'net.saliman:gradle-cobertura-plugin:2.0.0'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.3.1'
        classpath 'com.netflix.nebula:gradle-extra-configurations-plugin:2.2.+'
    }
}

plugins {
    id 'java-gradle-plugin'
    id "com.jfrog.bintray" version "1.7.3"
    id 'net.saliman.cobertura' version '2.2.5'
    id 'com.github.kt3k.coveralls' version '2.0.1'
}

repositories {
    mavenCentral()
    jcenter()
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'cobertura'
apply plugin: 'java-gradle-plugin'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'
apply plugin: 'com.google.protobuf'
apply plugin: 'provided-base'

cobertura.coverageFormats = ['html', 'xml']
cobertura.coverageSourceDirs = ['src/main/kotlin']

version = project.hasProperty('releaseVersion') ? project.releaseVersion : '0.0.1'
group = 'org.protokruft'
archivesBaseName = 'protokruft'
description = 'Kill all (Protobuf) builders!'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

compileKotlin.dependsOn('generateProto')

compileKotlin.kotlinOptions.jvmTarget = "1.8"
compileTestKotlin.kotlinOptions.jvmTarget = "1.8"

test {
    // Always run the tests
    outputs.upToDateWhen { false }

    // Turn on some console logging
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
        stackTraceFilters "entryPoint"
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task createClasspathManifest {
    def outputDir = file("$buildDir/$name")

    inputs.files sourceSets.main.runtimeClasspath
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        file("$outputDir/plugin-classpath.txt").text = sourceSets.main.runtimeClasspath.join("\n")
    }
}

dependencies {
    compile gradleApi()

    provided 'com.google.protobuf:protobuf-gradle-plugin:0.8.5'
    compile "org.reflections:reflections:0.9.11"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile 'com.google.protobuf:protobuf-java:3.5.1'
    compile('com.squareup:kotlinpoet:0.7.0') {
        exclude group: "org.jetbrains.kotlin"
    }
    testCompile localGroovy()
    testCompile 'junit:junit:4.12'
    testCompile 'com.natpryce:hamkrest:1.3.0.0'
    testCompile gradleTestKit()
    testRuntime files(createClasspathManifest)
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.0.0'
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.0.0-pre2'
        }
    }
    generateProtoTasks {
        ofSourceSet('main')*.plugins {
            grpc {}
        }
        ofSourceSet('test')*.plugins {
            grpc {}
        }
    }
}

sourceSets {
    main {
        proto {
            srcDirs += "src/main/proto"
        }
    }
    test {
        java {
            srcDirs += file("${protobuf.generatedFilesBaseDir}/main/java")
            srcDirs += file("src/example/java")
        }
        kotlin {
            srcDirs += file("src/test/kotlin")
            srcDirs += file("src/example/kotlin")
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = archivesBaseName

            pom.withXml {
                asNode().appendNode('name', archivesBaseName)
                asNode().appendNode('description', description)
                asNode().appendNode('url', 'http://' + archivesBaseName + '.github.io')
                asNode().appendNode('developers').appendNode('developer').
                        appendNode('name', 'David Denton').parent().
                        appendNode('email', 'david@http4k.org')
                asNode().appendNode('scm').
                        appendNode('url', 'git@github.com:daviddenton/' + archivesBaseName + '.git').parent().
                        appendNode('connection', 'scm:git:git@github.com:daviddenton/' + archivesBaseName + '.git').parent().
                        appendNode('developerConnection', 'scm:git:git@github.com:daviddenton/' + archivesBaseName + '.git')
                asNode().appendNode('licenses').appendNode('license').
                        appendNode('name', 'Apache License, Version 2.0').parent().
                        appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.html')
            }
            from components.java

            artifact sourcesJar
            artifact javadocJar
        }
    }
}

bintray {
    user = 'daviddenton'
    key = project.hasProperty('bintrayKey') ? project.bintrayKey : 'BINTRAYKEY'

    publications = ['mavenJava']
    dryRun = false
    publish = true
    pkg {
        repo = 'maven'
        name = archivesBaseName
        desc = description
        websiteUrl = 'https://github.com/daviddenton/' + archivesBaseName
        issueTrackerUrl = 'https://github.com/daviddenton/' + archivesBaseName + '/issues'
        vcsUrl = 'https://github.com/daviddenton/' + archivesBaseName + '.git'
        licenses = ['Apache-2.0']
        labels = []
        publicDownloadNumbers = false
        version {
            name = project.version
        }
    }
}